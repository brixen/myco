
import '../BasicSpec.my'
import '../../../lib/myco/core/Struct.my'


BasicSpec {
  name: "core/Struct"
  
  Subject < Struct {
    field foo: :foo_default
    field bar
    field baz: :baz_default
  }
  
  [tests]
  
  "fields gives all declared fields in order": {
    assert_equal(Subject.fields, [:foo, :bar, :baz])
  }
  
  "cast converts the given tuple into a struct": {
    tuple = Myco.tuple(:a, :b, :c)
    
    result = Subject.cast(tuple)
    assert_equal(tuple.__id__, result.__id__)
    assert_is_a(tuple, Subject)
    
    assert_equal(tuple[0], :a); assert_equal(tuple.foo, :a)
    assert_equal(tuple[1], :b); assert_equal(tuple.bar, :b)
    assert_equal(tuple[2], :c); assert_equal(tuple.baz, :c)
    
    tuple.foo = :A
    tuple.bar = :B
    tuple.baz = :C
    
    assert_equal(tuple[0], :A); assert_equal(tuple.foo, :A)
    assert_equal(tuple[1], :B); assert_equal(tuple.bar, :B)
    assert_equal(tuple[2], :C); assert_equal(tuple.baz, :C)
  }
  
  "cast uses default memes to fill unspecified fields": {
    tuple = Subject.cast(Myco.tuple(:a))
    
    assert_equal(tuple.foo, :a)
    assert_equal(tuple.bar, null)
    assert_equal(tuple.baz, :baz_default)
  }
}
