
import '../BasicSpec.my'


BasicSpec {
  name: "core/BasicDecorators"
  
  new_subject: BasicDecorators {
    var foo: Object { }
    memoize bar: |x,y,z| x+y-z
    sclass var baz: 99
  }
  
  [tests]
  
  "var decorator allows cached storage of a value": {
    subject = new_subject
    a = subject.foo
    b = subject.foo
    assert_equal(a, b)
  }
  
  "var decorator also declares a writer meme": {
    subject = new_subject
    a = Object { }
    b = subject.foo
    refute_equal(a, b)
    
    subject.foo = a
    b = subject.foo
    assert_equal(a, b)
  }
  
  "sclass decorator targets the singleton class of the component": {
    comp = new_subject.component
    
    assert_equal(comp.baz, 99)
    comp.baz = 0
    assert_equal(comp.baz, 0)
    comp.baz = 99
    assert_equal(comp.baz, 99)
  }
  
  "before decorator declares a wrapper that runs before the original": {
    collected = []
    subject = BasicDecorators {
      foo:        collected.push(88); 88
      before foo: collected.push(77); 77
    }
    
    assert_equal(subject.foo, 88)
    assert_equal(collected, [77, 88])
  }
  
  "after decorator declares a wrapper that runs after the original": {
    collected = []
    subject = BasicDecorators {
      foo:       collected.push(88); 88
      after foo: collected.push(99); 99
    }
    
    assert_equal(subject.foo, 88)
    assert_equal(collected, [88, 99])
  }
  
  "pre decorator declares a wrapper that pre-processes arguments": {
    collected = []
    subject = BasicDecorators {
      foo:     |x| collected.push(x); x % 8
      pre foo: |x| collected.push(x); [x + 1]
    }
    
    assert_equal(subject.foo(87), 0)
    assert_equal(collected, [87, 88])
  }
  
  "post decorator declares a wrapper that post-processes return value": {
    collected = []
    subject = BasicDecorators {
      foo:      |x| collected.push(x); x % 8
      post foo: |x| collected.push(x); x + 1
    }
    
    assert_equal(subject.foo(88), 1)
    assert_equal(collected, [88, 0])
  }
  
}
