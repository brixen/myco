
CodeTools::AST << {
  
  BuilderMethods << {
    invoke: |loc, receiver, name, arguments, block_params=null, block=null| {
      node = Invoke.new(
        line:      loc.line
        receiver:  receiver
        name:      name
        arguments: arguments
      )
      # TODO: be more clever with this mechanism
      # TODO: error if passing both block argument and block literal
      # Currently, this fails silently and ignores the block argument
      block && (
        node.arguments = node.arguments || ArgumentAssembly.new(line:loc.line, body:[])
        block_params = block_params || ParameterAssembly.new(line:loc.line)
        node.arguments.block = Iter.new(loc.line, block_params, block)
      )
      node
    }
  }
  
  # TODO: replace Iter and move this to Iter definition
  Iter << {
    bytecode: |g| {
      pos(g)
      
      state = g.state
      state.scope.nest_scope(self)
      
      blk = new_block_generator(g, self.arguments)
      
      blk.push_state(self)
      blk.definition_line(self.line)
      blk.state.push_super(state.super)
      blk.state.push_eval(state.eval)
      
      blk.state.push_name(blk.name)
      
      # Push line info down.
      pos(blk)
      
      self.arguments.bytecode(blk)
      
      blk.state.push_block
      blk.push_modifiers
      blk.break = null
      blk.next = null
      blk.redo = blk.new_label
      blk.redo.set!
      
      self.body.bytecode(blk)
      
      blk.pop_modifiers
      blk.state.pop_block
      blk.ret
      blk.close
      blk.pop_state
      
      blk.local_count = local_count
      blk.local_names = local_names
      
      g.create_block(blk)
    }
  }
  
  Invoke < Node, BasicObject {
    var receiver, var name, var arguments
    
    bytecode: |g| pos(g); implementation.bytecode(g)
    
    to_sexp: implementation.to_sexp
    
    implementation: {
      self.receiver.nil? && self.arguments.nil? &? (
        LocalVariableAccessAmbiguous.new(
          line: self.line
          name: self.name
        )
      ) ?? (
        InvokeMethod.new(
          line:      self.line
          receiver:  self.receiver || Self.new(line:self.line)
          name:      self.name
          arguments: self.arguments || ArgumentAssembly.new(line:self.line, body:[])
        )
      )
    }
  }
  
}
