
::Myco::BasicObject < ::Myco::EmptyObject, ::Myco::BasicDecorators {
  # Basic conditional handling
  if:     |cond, &blk| cond && blk.call
  unless: |cond, &blk| cond || blk.call
  case: |input, *rest| {
    # If there is a final strategy with no test value, use it as the else case
    use_strategy = rest.size.odd? && rest.pop
    # Iterate over the pairs of test_values and strategies,
    # selecting the first matching pair's strategy as the one to use.
    rest.each_slice(2).detect |test_value, strategy| {
      (test_value == input) && (
        use_strategy = strategy
        true
      )
    }
    # Call or return the selected strategy or error if none
    use_strategy &? (use_strategy.?call(input) ?? use_strategy)
                 ?? raise("No match for case("input")")
  }
  cond: |*rest| {
    # If there is a final strategy with no test value, use it as the else case
    use_strategy = rest.size.odd? && rest.pop
    # Iterate over the pairs of test_values and strategies,
    # selecting the first matching pair's strategy as the one to use.
    rest.each_slice(2).detect |test_value, strategy| {
      test_value && (
        use_strategy = strategy
        true
      )
    }
    # Call or return the selected strategy or error if none
    use_strategy &? (use_strategy.?call ?? use_strategy)
                 ?? raise("No match for cond")
  }
  
  switch: |input,comparator=:"=="| # TODO: remove deprecated switch
    Switch.new(input:input, comparator:comparator)
  
  # TODO: alias more efficiently
  # alias(::Kernel, :raise) raise
  # alias(::Kernel, :loop) loop
  raise: |*args| ::Kernel.instance_method(:raise).bind(self).call(*args)
  loop: |&block| ::Kernel.instance_method(:loop).bind(self).call(&block)
  break: raise(::StopIteration)
  
  puts: |*args| STDOUT.puts(*args)
  p:    |*args| STDOUT.puts(args.map |a| { a.inspect }.join(', '))
  
  ruby_require: |arg| Object.send(:require, arg)
}

# Because ::Myco::Instance is a ::BasicObject, we must shadow ::BasicObject here
::Myco::Instance::BasicObject: ::Myco::BasicObject

