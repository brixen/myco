
::Myco::Struct < ::Myco::BasicObject {
  static var fields: []
  
  static var struct_class: {
    component = self
    Ruby::Class.new(Rubinius::Tuple) {
      component.include_into(self)
      
      component.fields.each_with_index |field, index| {
        # Define getter method
        dynamic_method(field) |g| {
          g.push_self
            g.push_int(index)
          g.send(:"[]", 1)
          
          g.ret
        }
        
        # Define setter method
        dynamic_method(:""field"=") |g| {
          g.total_args = 1
          g.local_count = 1
          
          g.push_self
            g.push_int(index)
            g.push_local(0) # value
          g.send(:"[]=", 2)
          
          g.ret
        }
      }
    }
  }
  
  static cast: |tuple| {
    fields.size == tuple.size
    &? Rubinius::Unsafe.set_class(tuple, struct_class)
    ?? null
  }
  
  [decorators]
  
  field: FieldDecorator
  FieldDecorator: Decorator {
    apply: |meme| meme.target.fields.push(meme.name)
    [transforms]
    expose: false
  }
}
